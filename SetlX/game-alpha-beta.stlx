other := p |-> ([o : o in players() | o != p])[1];
value := cachedProcedure(State, player, alpha := -1, beta := 1) {
    if (finished(State)) {
        return utility(State, player);
    }
    val := alpha;
    for (ns in nextStates(State, player)) {
        val := max({ val, -value(ns, other(player), -beta, -val) });
        if (val >= beta) {
            return val;
        }
    }
    return val;
};
best_move := procedure(State, player) {
    bestVal := value(State, player);
    NS      := nextStates(State, player);
    return [bestVal, rnd({ns : ns in NS | -value(ns,other(player)) == bestVal})];
};
play_game := procedure() {
    State := startState();
    print(stateToString(State));
    while (!finished(State)) {
        start        := now();
        firstPlayer  := players()[1];
        [val, State] := best_move(State, firstPlayer);
        stop         := now();
        print(cacheStats(value));
        clearCache(value);
        print("For me, the game has the value $val$.  My move:");
        print(stateToString(State));
        print("computation took $(stop - start) * 0.001$ seconds");
        if (final_msg(State)) { return; }
        State := getMove(State);
        print(stateToString(State));
        if (final_msg(State)) { return; }
    }
};
