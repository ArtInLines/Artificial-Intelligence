value := cachedProcedure(State, player) {  // cachedProcedure for dynamic programming
    if (finished(State)) {
        return utility(State, player);
    }
    other := arb(players() - { player });
    return max({ -value(ns, other) : ns in nextStates(State, player) });
};
best_move := procedure(State, player) {
    NS      := nextStates(State, player);
    other   := arb(players() - { player });
    bestVal := value(State, player);
    return [bestVal, rnd({ns : ns in NS | -value(ns,other) == bestVal})];
};
play_game := procedure() {
    State := startState();
    print(stateToString(State));
    while (true) {
        start        := now();
        [val, State] := best_move(State, "O");
        stop         := now();
        print(cacheStats(value));
        print("For me, the game has the value $val$. My move:");
        print(stateToString(State));
        print("computation took $(stop - start) * 0.001$ seconds");
        if (final_msg(State)) { return; }
        State := getMove(State);
        print(stateToString(State));
        if (final_msg(State)) { return; }
    }
};
