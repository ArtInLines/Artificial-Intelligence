// This program implements the minimax algorithm.
other := p |-> ([o : o in players() | o != p])[1];
value := cachedProcedure(State, player) {  // cachedProcedure for dynamic programming
    if (finished(State)) {
        return utility(State, player);
    }
    return max({ -value(ns, other(player)) : ns in nextStates(State, player) });
};
best_move := procedure(State, player) {
    NS      := nextStates(State, player);
    bestVal := value(State, player);
    return [bestVal, rnd({ns : ns in NS | -value(ns,other(player)) == bestVal})];
};
play_game := procedure() {
    State := startState();
    print(stateToString(State));
    while (true) {
        start        := now();
        firstPlayer  := players()[1];
        [val, State] := best_move(State, firstPlayer);
        stop         := now();
        print(cacheStats(value));
        print("For me, the game has the value $val$. My move:");
        print(stateToString(State));
        print("computation took $(stop - start) * 0.001$ seconds");
        if (finished(State)) {
            final_msg(State);
            break;
        }
        State := getMove(State);
        print(stateToString(State));
        if (finished(State)) {
            final_msg(State);
            break;
        }
    }
};
