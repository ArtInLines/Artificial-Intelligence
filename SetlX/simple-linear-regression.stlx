load("table.stlx");

simple_linear_regression := procedure(fileName, types, name) {
    csv  := readTable(fileName, types);
    data := csv.getData();
    m    := #data;
    idx  := find_index(name, csv.getColumnNames());
    Y    := [];
    X    := [];
    L    := [1 .. m];
    for (i in L) {
        Y[i] := 1 / data[i][1];
        X[i] := data[i][idx];
    }
    xMean  := +/ X / m;
    yMean  := +/ Y / m;
    theta1 := (+/ [(X[i]-xMean)*(Y[i]-yMean) : i in L]) / (+/ [(X[i]-xMean)**2 : i in L]);
    theta0 := yMean - theta1 * xMean;
    TSS    := +/ [(Y[i] - yMean) ** 2 : i in L];
    RSS    := +/ [(theta0 + theta1 * X[i] - Y[i]) ** 2 : i in L];
    R2     := 1 - RSS / TSS;
    XSS    := +/ [(X[i] - xMean) ** 2 : i in L];
    t      := abs(theta1) * sqrt((m-2) / RSS * XSS);
    p      := 1 - stat_studentCDF(t, m-2);

    
    xMin   := 0.5 * min(X);
    yMin   := theta0 + theta1 * xMin;
    xMax   := 1.1 * max(X);
    yMax   := theta0 + theta1 * xMax;
    canvas := plot_createCanvas("Fuel consumption vs. $name$: R2 = $toShort(R2)$");
    plot_addBullets(canvas, [ [X[i], Y[i]] : i in L ], [0,0,255], 2.0);
    plot_addListGraph(canvas, [[xMin, yMin], [xMax, yMax]], "regression line", [255,0,0]);
    plot_labelAxis(canvas, name, "fuel consumption (gallons/mile)");
    plot_modSize(canvas, [1000, 1000]);

    print(t, p);
    return [R2, p];
};
// Find the index of x in List.
find_index := procedure(x, List) {
    return arb({ idx : idx in [1 .. #List] | List[idx] == x });
};
toShort := x |-> round(x * 1000) * 0.001;
test := procedure() {
    types := ["double", "int", "double", "double", "double", "double", "int", "string"];
    for (varName in ["displacement", "cyl", "hp", "weight", "acc", "year"]) {
        [R2, p] := simple_linear_regression("cars.csv", types, varName);
        print("The explained variance for $varName$ vs fuel consumption is $R2$, p value: $p$.");
        get("Press Enter to continue");
    }
};

test();
