load("game.stlx");
// load("game-alpha-beta.stlx");
// load("game-limit.stlx");

// nullary functions are a hack to emulate global variables
players    := [] |-> [ "X", "O" ]; 
startState := [] |-> [ [ " " : col in [1..3]] : row in [1..3] ];
nextStates := procedure(State, player) {
    Empty  := empty(State);
    Result := {};
    for ([row, col] in Empty) {
        NextState           := State;
        NextState[row][col] := player;
        Result              += { NextState };
    }
    return Result;
};
empty := S |-> {[row,col] : row in [1..3], col in [1..3] | S[row][col] == " "};
utility := procedure(State, player) {
    for (Pairs in all_lines()) {
        Marks := { State[row][col] : [row, col] in Pairs };
        if (#Marks == 1 && Marks != { " " }) {
            if (Marks == { player }) { return  1; } else { return -1; }
        }
    }
    if (forall(row in [1..3], col in [1..3] | State[row][col] != " ")) {
        return 0;   
    }
};
all_lines := closure() {
    Lines := { { [row, col] : col in [1..3] } : row in [1..3] };
    Lines += { { [row, col] : row in [1..3] } : col in [1..3] };
    Lines += { { [idx, idx] : idx in [1..3] } };
    Lines += { { [1, 3], [2, 2], [3, 1] } };
    return Lines;
};
finished := procedure(State) {
    return utility(State, "X") != om;
};
////////////////////////////////////////////////////////////////////////////////
stateToString := procedure(State) {
    L     := [1 .. 3];
    ls    := "\n" + "-" * (4 * 3 + 1) + "\n";  // line separator
    board := join(["| " + join([State[r][c]: c in L]," | ") + " |": r in L], ls);
    return ls + board + ls;
};
getMove := procedure(State) {
    [row, col] := split(read("Your turn, enter move as row, col: "), " *, *");
    [row, col] := [int(row), int(col)];
    State[row][col] := "O";
    return State;
};
final_msg := procedure(State) {
    if (finished(State)) {
        if (utility(State, "O") == 1) {
            print("You have won!");
        } else if (utility(State, "O") == -1) {
            print("You have lost!");
        } else {
            print("It's a draw.");
        }
        return true;
    }
    return false;
};
for (Pairs in all_lines()) { print(Pairs); }
play_game();

